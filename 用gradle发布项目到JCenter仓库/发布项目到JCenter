1. 在https://bintray.com/上注册一个账号

2. 注册后，在https://bintray.com/profile/edit页面的左边菜单的最下面有API Key,点击show，记下它，下面会用到

3. 在项目新建一个lib module, 在项目的（非module）build.gradle的dependencies标签下添加：

classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'

4. 在lib module的build.gradle配置如下内容：

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
version = "1.0.0"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    resourcePrefix "watchpicture_________"	//随便填

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/universal-image-loader-1.9.4.jar')
    compile 'com.android.support:support-v4:23.1.1'
}


def siteUrl = 'https://github.com/DengGCccc/watchpicture'    // project homepage, 可以在Github里随便创建一个仓库
def gitUrl = 'https://github.com/DengGCccc/watchpicture.git' // project git

group = "cn.dgc.open"		//cn.dgc.open:watchpicture:1.0.0

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'WatchPicture For Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {	//开发者的基本信息
                        id 'cg'
                        name 'Denggc'
                        email 'denggc@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "watchpicture"                // project name in jcenter
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

5. 在项目的根目录的local.properties里添加（bintray.user要填Bintray的账户名，非Name (如cg (DengGC))）
bintray.user=cg
bintray.apikey=fcaa33d2083a091fbcdd7e37f1b05a17af0d7653

6. 项目的命令行输入gradlew install ，返回BUILD SUCCESS

7. 项目的命令行输入gradlew bintrayUpload ，返回BUILD SUCCESS， 此时项目已经上传到JCenter了

8. 向JCenter发出申请， 在https://bintray.com/bintray/jcenter 这个页面点击“ Include My Package”， 输入你项目的关键字，选中后，点击勾，输入comment信息，点击send。等待审核（一个钟左右）

